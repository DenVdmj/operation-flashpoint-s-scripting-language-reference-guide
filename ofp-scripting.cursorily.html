﻿<!doctype html>
<html>
<head>
<meta charset="utf-8">
<title>Язык описания сценариев игры Operation Flashpoint. Галопом по европам.</title>
<link href=".rsc/style.css" rel="stylesheet" type="text/css" />
<script src=".rsc/highlight.pack.js"></script>
<script src=".rsc/init.js"></script>
<script src="hyphened-text.js"></script>
</head>
<body>

<ul class="toc">
  <li><a href="#toc01">Галопом по европам</a>
    <ul>
      <li><a href="#toc02">Некоторые ошибки начинающего скриптовальщика</a>
        <ul>
          <li><a href="#toc03">Совсем ужасный код</a></li>
          <li><a href="#toc04">Никогда не используйте копипаст кода</a></li>
          <li><a href="#toc05">Выносите многократно повторяющиеся действия в подпрограммы</a></li>
          <li><a href="#toc06">Разделяйте код и данные</a></li>
          <li><a href="#toc07">Ваши структуры данных важнее вашего кода</a></li>
          <li><a href="#toc08">Никогда не дублируйте данные</a></li>
          <li><a href="#toc09">Храните данные связанные по смыслу в одном месте</a></li>
          <li><a href="#toc10">SQS vs SQF?</a></li>
        </ul>
      </li>
      <li><a href="#toc11">Глобальные имена, чем это плохо и как с этим бороться</a>
        <ul>
          <li><a href="#toc12">Трудности с глобальными именами</a>
            <ul>
              <li><a href="#toc13">Mangled-Style</a></li>
              <li><a href="#toc14">Переменные передаваемые по ссылке от родительского скрипта дочерним</a></li>
              <li><a href="#toc15">Библиотеки возвращающие список определяемых функций</a></li>
            </ul>
          </li>
          <li><a href="#toc16">На что уходит время?</a></li>
          <li><a href="#toc17">Сортировка</a></li>
        </ul>
      </li>
    </ul>
  </li>
</ul>

<h2 id="toc01">Галопом по европам</h2>

<p>Это приложение несколько отступает от основной идеи справочника —
кратко описывать интересные и тонкие моменты языка.
Я слегка отвлекусь, и в свободной форме покомментирую всякие вещи по поводу которых мне есть что сказать.</p>

<h3 id="toc02">Некоторые ошибки начинающего скриптовальщика</h3>

<h4 id="toc03">Совсем ужасный код</h4>

<pre class="hl sqf">; увеличивает число на единицу

? a == 0 : b = 1; goto "end"
? a == 1 : b = 2; goto "end"
? a == 2 : b = 3; goto "end"
? a == 3 : b = 4; goto "end"
? a == 4 : b = 5; goto "end"
? a == 5 : b = 6; goto "end"
? a == 6 : b = 7; goto "end"
? a == 7 : b = 8; goto "end"
? a == 8 : b = 9; goto "end"
? a == 9 : b = 10; goto "end"

#end</pre>

<p>Узнаете? На таком коде бывает даже ставят своё имя!</p>

<pre class="hl sqf">; Скрипт увеличения числа на единицу. DenVdmj (c) 2006
; Если Вам понравилася эта ИДЕЯ вы можете использовать её в своих скриптах,
; только не забудте указать МЕНЯ как АВТОРА этой ИДЕИ! Я думаю Вы поняли,
; что все ОЧЕНЬ ПРОСТО, точно также можно увеличивать на ДВОЙКУ и на ТРОЙКУ
; и на ДРУГИЕ ЧИСЛА!

? CHISLO == 0 : RESULTAT = 1; goto "KONETS"
? CHISLO == 1 : RESULTAT = 2; goto "KONETS"
? CHISLO == 2 : RESULTAT = 3; goto "KONETS"
? CHISLO == 3 : RESULTAT = 4; goto "KONETS"
? CHISLO == 4 : RESULTAT = 5; goto "KONETS"
? CHISLO == 5 : RESULTAT = 6; goto "KONETS"
? CHISLO == 6 : RESULTAT = 7; goto "KONETS"
? CHISLO == 7 : RESULTAT = 8; goto "KONETS"
? CHISLO == 8 : RESULTAT = 9; goto "KONETS"
? CHISLO == 9 : RESULTAT = 10; goto "KONETS"

#KONETS</pre>

<p>Вам смешно? Мне — нисколько, ведь люди действительно с удовольствием пользуются.
Я обнаглею, и, невзирая на объем такого кода, распишу его здесь полностью:</p>

<pre class="hl sqf">_unit = _this select 0
_num = random(10)
?_num &gt;= 9: _unit setObjectTexture [0, "\PathTo\Sign9.paa"], goto "num2"
?_num &gt;= 8: _unit setObjectTexture [0, "\PathTo\Sign8.paa"], goto "num2"
?_num &gt;= 7: _unit setObjectTexture [0, "\PathTo\Sign7.paa"], goto "num2"
?_num &gt;= 6: _unit setObjectTexture [0, "\PathTo\Sign6.paa"], goto "num2"
?_num &gt;= 5: _unit setObjectTexture [0, "\PathTo\Sign5.paa"], goto "num2"
?_num &gt;= 4: _unit setObjectTexture [0, "\PathTo\Sign4.paa"], goto "num2"
?_num &gt;= 3: _unit setObjectTexture [0, "\PathTo\Sign3.paa"], goto "num2"
?_num &gt;= 2: _unit setObjectTexture [0, "\PathTo\Sign2.paa"], goto "num2"
?_num &gt;= 1: _unit setObjectTexture [0, "\PathTo\Sign1.paa"], goto "num2"
?_num  &lt; 1: _unit setObjectTexture [0, "\PathTo\Sign0.paa"], goto "num2"

#num2
_num = random(10)
?_num &gt;= 9: _unit setObjectTexture [1, "\PathTo\Sign9.paa"], goto "num1"
?_num &gt;= 8: _unit setObjectTexture [1, "\PathTo\Sign8.paa"], goto "num1"
?_num &gt;= 7: _unit setObjectTexture [1, "\PathTo\Sign7.paa"], goto "num1"
?_num &gt;= 6: _unit setObjectTexture [1, "\PathTo\Sign6.paa"], goto "num1"
?_num &gt;= 5: _unit setObjectTexture [1, "\PathTo\Sign5.paa"], goto "num1"
?_num &gt;= 4: _unit setObjectTexture [1, "\PathTo\Sign4.paa"], goto "num1"
?_num &gt;= 3: _unit setObjectTexture [1, "\PathTo\Sign3.paa"], goto "num1"
?_num &gt;= 2: _unit setObjectTexture [1, "\PathTo\Sign2.paa"], goto "num1"
?_num &gt;= 1: _unit setObjectTexture [1, "\PathTo\Sign1.paa"], goto "num1"
?_num  &lt; 1: _unit setObjectTexture [1, "\PathTo\Sign0.paa"], goto "num1"

#num1
_num = random(10)
?_num &gt;= 9: _unit setObjectTexture [2, "\PathTo\Sign9.paa"], goto "exit"
?_num &gt;= 8: _unit setObjectTexture [2, "\PathTo\Sign8.paa"], goto "exit"
?_num &gt;= 7: _unit setObjectTexture [2, "\PathTo\Sign7.paa"], goto "exit"
?_num &gt;= 6: _unit setObjectTexture [2, "\PathTo\Sign6.paa"], goto "exit"
?_num &gt;= 5: _unit setObjectTexture [2, "\PathTo\Sign5.paa"], goto "exit"
?_num &gt;= 4: _unit setObjectTexture [2, "\PathTo\Sign4.paa"], goto "exit"
?_num &gt;= 3: _unit setObjectTexture [2, "\PathTo\Sign3.paa"], goto "exit"
?_num &gt;= 2: _unit setObjectTexture [2, "\PathTo\Sign2.paa"], goto "exit"
?_num &gt;= 1: _unit setObjectTexture [2, "\PathTo\Sign1.paa"], goto "exit"
?_num &gt;= 0: _unit setObjectTexture [2, "\PathTo\Sign0.paa"], goto "exit"

#exit
exit</pre>

<p>И это только для трех разрядов числа!
Можно только теряться в догадках откуда берутся такие «гениальные»
вещи и насколько надо быть занятым человеком, чтобы это использовать.
Нашему «занятому человеку» придется, конечно, прибегнуть к автозамене и копипасту, с последующей дурацкой правкой,
чтобы подогнать это «чудо» под свои нужды.
А ведь все решение для абсолютно любых входных параметров (сам номер, имена текстур и секции) умещается
в несколько строк кода. И это делается один раз на всю жизнь и на любые проекты.</p>

<pre class="hl sqf">_object =
_number =
_texturePathMask = "\Path\To\Signatures\Sign%1colorRed.paa";
_sections = [5,4,3,2,1,0]; // секции перечисленные от младшего
                           // разряда к старшему, т.е. слева направо
{
    // отбросить дробную часть
    _number = _number - _number % 1;
    // установить текстуру c цифрой, остаток деления _number на 10
    // вернёт младший десятичный разряд числа
    _object setObjectTexture [_x, format [_texturePathMask, _number % 10]];
    // сдвинуть _number на один десятичный разряд вправо
    _number = _number / 10
} foreach _sections;</pre>

И еще раз для самых занятых, в виде скрипта принимающего параметры:
<pre class="hl sqf">;// args: [объект, номер, маска пути/имени файла сигнатуры, массив секций]
_num = _this select 1;
{
    _num = _num - _num % 1;
    _this select 0 setObjectTexture [_x, format [_this select 2, _num % 10]];
    _num = _num / 10
} foreach (_this select 3)</pre>
<p>Можете поудалять переводы строк чтобы вызывать это командой exec.</p>

<p>Вот еще один совершенно реальный образчик стиля которым написано немало скриптов:</p>

<pre class="hl sqf">#loop
_dam=getdammage _unit
?(_dam==0) : goto "start"
?(_dam==1) : goto "end"
?(_dam&lt;=0.1):_unit setdammage _dam+0,01
?(_dam&lt;=0.2) &amp;&amp; (_dam&gt;0.1):_unit setdammage (_dam+0.02)
?(_dam&lt;=0.3) &amp;&amp; (_dam&gt;0.2):_unit setdammage (_dam+0.03)
?(_dam&lt;=0.4) &amp;&amp; (_dam&gt;0.3):_unit setdammage (_dam+0.04)
?(_dam&lt;=0.5) &amp;&amp; (_dam&gt;0.4):_unit setdammage (_dam+0.05)
?(_dam&lt;=0.6) &amp;&amp; (_dam&gt;0.5):_unit setdammage (_dam+0.06)
?(_dam&lt;=0.7) &amp;&amp; (_dam&gt;0.6):_unit setdammage (_dam+0.07)
?(_dam&lt;=0.8) &amp;&amp; (_dam&gt;0.7):_unit setdammage (_dam+0.08)
?(_dam&lt;=0.9) &amp;&amp; (_dam&gt;0.8):_unit setdammage (_dam+0.09)
?(_dam&gt;0.9):_unit setdammage (_dam+0.1)
~10
goto "loop"</pre>
<p>Совершенно не важен именно этот код — дело в том, что такой подход процветает среди начинающих скриптовать.</p>

<p>Никогда так не делайте — придумайте свою супер-формулу здоровья, или чего угодно, что вы собираетесь
решать подобным образом, но пишите скрипты по человечески.</p>

<h4 id="toc04">Никогда не используйте копипаст кода</h4>
<p>Это не только убивающая мысль рутина, но и частый источник ошибок — обычно Вам нужно изменить
пару-тройку переменных, или еще что-то чтобы подогнать его под текущие задачи, и забыв незначительную
деталь Вы потратите на выяснение причины некоторое время, которое можно было потратить с пользой.</p>

<h4 id="toc05">Выносите многократно повторяющиеся действия в подпрограммы</h4>
<p>Вместо того чтобы писать (или копипастить) один и тотже код десятки раз, используйте функции — они
именно для этого и предназначены.</p>

<h4 id="toc06">Разделяйте код и данные</h4>

<p>Не рассовывайте данные по скриптам, помните о том, что однажды Вы захотите
изменить тот набор стволов, который добавляется в машины, захотите изменить цены на
оружие в вашей палатке торговца оружием — помните о тех несчастных которые хотят использовать
ваши скрипты с другими аддонами.</p>

<p>Никогда не делайте так:</p>

<pre class="hl sqf">_car addWeaponCargo ["SomeGun",1];
_car addWeaponCargo ["OtherGun",1];
_car addWeaponCargo ["SuperGun",1];
_car addWeaponCargo ["SuperPuperGun",1];
// еще с десяток раз
_car addWeaponCargo ["SuperPuperMegaGun",1]</pre>
<p>Выносите данные о стволах в файлы с данными:</p>

<pre class="hl sqf">[
   "SomeGun",
   "OtherGun",
   "SuperGun",
   "SuperPuperGun",
   "SuperPuperMegaGun"
]</pre>
<p>И используйте</p>

<pre class="hl sqf">{
    _car addWeaponCargo [_x, 1]
} foreach call preprocessFile "resources.sqf"</pre>

<p>Вы легко можете улучшить скрипт, храня магазины и прочие относящиеся к оружию данные вместе:</p>

<pre class="hl sqf">[
   ["SomeGun", "SomeGunMag"],
   ["OtherGun", "OtherGunMag"],
   ["SuperGun", "SuperGunMag"],
   ["SuperPuperGun", "SuperPuperGunMag"],
   ["SuperPuperMegaGun", "SuperPuperMegaGunMag"]
]</pre>
<pre class="hl sqf">{
    _car addWeaponCargo [_x select 0, 1];
    _car addMagazineCargo [_x select 1, 4]

} foreach call preprocessFile "resources.sqf"</pre>
<p>Используйте такой подход даже если ваш скрипт очень простой; если Вы не хотите использовать
лишние файлы, то хотя-бы выносите данные в начало скрипта, до любого кода,
так чтобы Ваши пользователи смогли без мучений добраться до них.</p>

<h4 id="toc07">Ваши структуры данных важнее вашего кода</h4>
<p>Больше думайте над тем как хранить данные, чем как написать какой-то там цикл.
Если Вы правильно выбрали структуры данных (увы, в офп это всегда приходится имитировать на массивах,
что усложняет хороший выбор) Вы всегда легко напишите любой код для работы с ними.</p>

<h4 id="toc08">Никогда не дублируйте данные</h4>
<p>Нельзя дублировать данные, это элементарно чревато частыми ошибками —
изменив нечто в одном месте, но забыв внести изменения в другом, Вы получите неоднозачность,
и трудно обнаруживаемые глюки.</p>

<h4 id="toc09">Храните данные связанные по смыслу в одном месте</h4>
<p>Старайтесь выделять сущности, так например автомат — сущность,
а его название, вес, цвет, вкус, запах, совместимые с ним прицелы, магазины которые он
использует — его аттрибуты.</p>

<p>Это просто удобно — так как позволяет легко обнаруживать их взаимосвязь,
и соответственно находить как все аттрибуты некой сущности так и наоборот — принадлежность аттрибута.</p>

<p>Чтобы показать как использовать этот совет на практике, приведу один простой пример.</p>

<p>Представте себе табличку:</p>

<pre class="hl sqf">_weapons =
//     gun            magazine       optic   silencer
[
     ["ICP_val",     "ICP_valmag",   "",     "integrated"],
     ["ICP_valpso",  "ICP_valmag",   "pso",  "integrated"],
     ["ICP_vss",     "ICP_vssmag",   "pso",  "integrated"],
     ["ICP_ak74",    "ICP_ak74mag",  "",     ""          ],
     ["ICP_ak74mo",  "ICP_ak74mag",  "pso",  ""          ]
]</pre>
<p>Давайте напишем пару-тройку полезных функций для работы с такой табличкой.
В подобных табличках лишь один столбец содержит гарантированно уникальные значения,
если это не так, то это какая то другая табличка )).
Значения такого столбца можно назвать ключами, в нашем случае это столбец с названиями стволов, пусть
следующая функция будет возвращать нам ряд по ключу (названию ствола):</p>

<pre class="hl sqf">_getByKey = { // args: table, key value
    private ["_tbl", "_key", "_i"];
    _tbl = _this select 0;
    _key = _this select 2;
    _i = 0;
    while { _key != (_tbl select _i) select 0 } do { _i = _i + 1 };
    if ( _i &lt; count _tbl ) then { _tbl select _i } else { [] }
}</pre>
<p>Назначение такой функции весьма понятно — вызов:</p>

<pre class="hl sqf output">([_weapons, "ICP_vss"] call _getByKey) select 1</pre>
<p>вернёт название магазина для винтореза, а вызов</p>

<pre class="hl sqf output">([_weapons, "ICP_vss"] call _getByKey) select 3</pre>
<p>название его глушителя.</p>

<p>Еще одна важная операция — это получение всех рядов по совпавшим значениям в определенных столбцах,
это поможет нам в ситуации когда мы захотим получить все стволы которые используют определенный магазин или оптику:</p>

<pre class="hl sqf">_selectByColumn = { // args: table, column, value
    private ["_tbl", "_col", "_val", "_sel"];
    _tbl = _this select 0;
    _col = _this select 1;
    _val = _this select 2;
    _sel = [];
    {
        if ( _x select _col == _val ) then { _sel set [count _sel, _x] }
    } foreach _tbl;
    _sel // return selection
}</pre>
<p>Ок, теперь вызов:</p>

<pre class="hl sqf output">[_weapons, 1, "ICP_ak74mag"] call _selectByColumn</pre>
<p>возвращает массив:</p>

<pre class="hl sqf output">[
     ["ICP_ak74",   "ICP_ak74mag", "",    ""],
     ["ICP_ak74mo", "ICP_ak74mag", "pso", ""]
]</pre>
<p>а вызов:</p>

<pre class="hl sqf output">[_weapons, 3, "integrated"] call _selectByColumn</pre>
<p>массив:</p>

<pre class="hl sqf output">[
     ["ICP_val",    "ICP_valmag", "",    "integrated"],
     ["ICP_valpso", "ICP_valmag", "pso", "integrated"],
     ["ICP_vss",    "ICP_vssmag", "pso", "integrated"],
]</pre>
<p>Аналогично можно найти все шумные стволы:</p>

<pre class="hl sqf output">[_weapons, 3, ""] call _selectByColumn</pre>
<p>все стволы с оптикой ПСО:</p>

<pre class="hl sqf output">[_weapons, 2, "pso"] call _selectByColumn</pre>
<p>или без оптического прицела:</p>

<pre class="hl sqf output">[_weapons, 2, ""] call _selectByColumn</pre>
<p>Вы можете существенно расширить возможности _selectByColumn если измените её так, чтобы
вместо простого значения она принимала калбэк функцию определяющую подходит нам этот ряд или нет.
Итак создадим еще одну функцию:</p>

<pre class="hl sqf">_selectByCondition = { // args: table, condition
    private "_sel"; _sel = [];
    {
        if ( _x call (_this select 1) ) then { _sel set [count _sel, _x] }
    } foreach (_this select 0);
    _sel // return selection
};</pre>
<p>Эта функция совсем небольшая, так как всю проверку мы вытащили наружу, и казалось бы,
что можно обойтись совсем без этой функции, но все же с ней запрос выглядит осмысленней:</p>

<pre class="hl sqf output">[
    _weapons,
    {
        _this select 2 == "pso" &amp;&amp;
        _this select 3 == "integrated"
    }
] call _selectByCondition</pre>
<p>этот вызов вернёт такой массив:</p>

<pre class="hl sqf output">[
    ["ICP_valpso", "ICP_valmag", "pso", "integrated"],
    ["ICP_vss",    "ICP_vssmag", "pso", "integrated"]
]</pre>
<p>Кстати, имейте ввиду, что возвращаемые ряды — это не копии рядов таблицы, а ссылки на них.
Если вы измените значения в них, то изменится и сама таблица.</p>

<p>Все эти функции можно использовать с любым количеством самых разных таких табличек.
Используйте их, или придумайте свои, более мощные и удобные, пусть данные вашего проекта храняться грамотно.</p>

<p>Вы все еще используете жуткие наборы из нескольких десятков</p>

<pre class="hl sqf output">? _ствол == "стволнэйм" : _оптика = <em>true</em>; exit</pre>
или что-то в этом духе? Тогда мы идем к Вам!


<h4 id="toc10">SQS vs SQF?</h4>

<p>Наверное Вы заметили — чтобы иметь возможность выполнить эти советы, Вам нужно писать
используя sqf. Если Вы считаете, что можете легко и красиво писать sqs,
что Вас не будет смущать невозможность удобно создавать подпрограммы, и Вы легко врубаетесь в
нагромаждение непонятно куда ведущих гоуту — чтож, отправляйтесь кодить в асм, Вы
один из тех гениев, что предпочитают низкоуровневые языки. Не вышло? Тогда не обманывайте себя и
учите sqf, то что Вам сейчас кажется простым, просто лишь до тех пор пока Ваши скрипты не требуют большего.</p>

<h3 id="toc11">Глобальные имена, чем это плохо и как с этим бороться</h3>

<h4 id="toc12">Трудности с глобальными именами</h4>

<p>Первое что Вы сами можете заметить — это то, что при совместном использовании скриптов
написанных разными людьми некоторые глобальные имена могут случайно совпасть.
Так же надо помнить, что глобальное простраство имен в игре помимо переменных включает
все имена юнитов, триггеров, а также имена команд.</p>

<p>Если Ваш скрипт совсем прост, то необходимость для его работы глобальной переменной — это
непозволительная роскошь, и напрасное замусоревание общего простраства имен.</p>

<p>Если то что вы пишите — сложная скриптовая система, то есть и более серьезные причины
стараться меньше использовать глобальные имена.
Любой скрипт использующий некоторый глобальный ресурс становится зависимым от него.
Это значит, что если на некотором этапе вы захотите изменить способ хранения некоторых таких данных,
Вам придется переписывать все заново. Если таких зацепок по глобальным именам (хуже — по структурам данных)
будет слишком много, то все они просто зацементируют проект, его очень трудно будет расширить не превратив
в мусорное месиво кода и данных, и практически невозможно модифицировать.</p>

<h5 id="toc13">Mangled-Style</h5>

<p>Это не решение самой проблемы, но самый простой и эфективный способ не допустить бардака и
путаницы с глобальными именами. Несмотря не заумное название все очень
просто — придумайте схему для всех имен проекта и давайте имена только в соответсвии с ней.
Другими словами, пусть ваши имена будут сложносоставными
(такими как это слово) и будут отражать не только своё предназначение, но и принадлежность (моду, проекту, аддону,
скриптовой системе, её модулю и далее по нисходящей).</p>

<p>Допустим наш проект называется «My Super Puper Modification» сокращенно MSPMOD, этот
проект реализует, помимо прочего, систему (просто несколько скриптов) отслеживания состояния
игрока и имеет соответствуещее название — TracePlayer — тогда все
глобальные переменные используемые этим модулем будут иметь подобные имена:</p>

<pre class="hl sqf">MSPMOD_TracePlayer_currentVehicle
MSPMOD_TracePlayer_usedVehicle
MSPMOD_TracePlayer_usedWeapons
MSPMOD_TracePlayer_killedEnemy</pre>

<p>Можно использовать в качестве склеивающих символов двойное подчеркивание «__».</p>

<h5 id="toc14">Переменные передаваемые по ссылке от родительского скрипта дочерним</h5>
<p>Часто нескольким параллельно выполняющимся скриптам нужен доступ к одному набору данных,
и это побуждает нас хранить эти данные в глобальной переменной.
Однако если существует возможность стартовать наши скрипты из некоторого общего места —
другого «родительского» скрипта, то вполне можно обойтись локальными переменными.</p>

<pre class="hl sqf">// массив с именами скриптов
_processes = [];

// массив с любыми данными
_sharedData = [];

// каждый из запущенных процессов получит доступ к _sharedData
{ _sharedData exec _x } foreach _processes;</pre>
<p>Таким образом массив _sharedData становится общей областью данных как для текущего
так и для дочерних процессов. Помните об этом способе, и если в Вашем случае
он может заменить глобальные переменные — используйте его.</p>

<h5 id="toc15">Библиотеки возвращающие список определяемых функций</h5>
<p>Функции тоже имеют имена, и их иногда тоже хочется сделать глобальными,
чтобы не таскать их по десять раз из файла preprocessFile'ом. Можно давать им префикс в духе «globFunc»
или просто «func», в таком случае удобно расположить все функции в одном файле:</p>

<pre class="hl sqf">#define arg(x) = (_this select x)

// [getpos unit, circlePosition, circleRadius] call funcInCircle

funcInCircle = {
    #define mDeltaX ((_ppos select 0) - (_cpos select 0))
    #define mDeltaY ((_ppos select 1) - (_cpos select 1))
    #define mRadius (arg(2))
    private ["_ppos","_cpos"];
    _ppos = arg(0);
    _cpos = arg(1);
    mRadius ^ 2 &gt; mDeltaX ^ 2 + mDeltaY ^ 2
};

// [getpos unit1, getpos unit2] call funcGetDirToPos

funcGetDirToPos = {
    private ["_p1","_p2","_dx","_dy"];
    _p1 = arg(0);
    _p2 = arg(1);
    _dx = (_p1 select 0) - (_p2 select 0);
    _dy = (_p1 select 1) - (_p2 select 1);
    if ( _dx == 0 &amp;&amp; _dy == 0 ) then {
        -360 // for calling side: error if negative
    } else {
        ( 180 + (_dx atan2 _dy) ) % 360
    }
};

// и так далее...</pre>
<p>Но раз уж мы взялись бороться с глобальными именами, то решим,
что не все функции бывают нужны часто,
и иногда проще загрузить один такой набор функций:</p>

<pre class="hl sqf">// файл library.libname.sqf
_library_libname =
{
    _funcInCircle = {
        ...
    };

    _funcGetDirToPos = {
        ...
    };

    _funcNearestFromList = {
        ...
    };
};

["_funcInCircle", "_funcGetDirToPos", "_funcNearestFromList"]</pre>
<p>При таком подходе переменные вида _library_libname всегда соответствуют названию файла библиотки "library.libname.sqf",
и подключение библиотечных функций выглядит так:</p>

<pre class="hl sqf">private "_library_libname";
private call preprocessFile "library.libname.sqf";
call _library_libname;</pre>
<p>Но в действительности, это удобней только тогда, когда мы не хотим использовать глобальные функции.
Можно значительно повысить удобство используя компромиссный вариант:</p>

<pre class="hl sqf">MSPMOD_LIBRARY_LIBNAME_EXPORT =
[
    "_mspmod_library_libname",
    "_funcInCircle",
    "_funcGetDirToPos",
    "_funcNearestFromList"
];

MSPMOD_LIBRARY_LIBNAME =
{
    _funcInCircle = {
        ...
    };

    _funcGetDirToPos = {
        ...
    };

    _funcNearestFromList = {
        ...
    };
};</pre>
<p>Мы лишь однажды вызываем инициализацию такой библиотеки, непосредственно при старте проекта:</p>

<pre class="hl sqf">call preprocessFile "my.lib.sqf"</pre>
<p>И всегда можем получить в текущий контекст определенные в ней функции в два движения:</p>

<pre class="hl sqf">private MSPMOD_LIBRARY_LIBNAME_EXPORT;
call MSPMOD_LIBRARY_LIBNAME;</pre>
<p>В таком случае мы сокращаем количество глобальных имен до двух на каждую библиотеку,
и вовсе ничего не таскаем раз за разом из файла — это делается только однажды.
И если бы весь код заключенный в фигурные скобки «{}» транслировался в промежуточное
представление, то такой способ был бы практически столь же быстрым как и вариант с глобальными функциями.
Можно «экспортировать» и только нужные функции:</p>

<pre class="hl sqf">private ["_funcInCircle", "_funcNearestFromList"];
call MSPMOD_LIBRARY_LIBNAME;</pre>
<p>Нужно только помнить, что если в текущем контексте уже есть имена совпадающие с именами
определёнными в библиотеке, они будут перезаписаны вызовом MSPMOD_LIBRARY_LIBNAME.</p>

<p>Ну и можно сказать, что несмотря на, мягко говоря, своеобразность этих забавных методов,
они иногда действительно облегчают жизнь.</p>

<h4 id="toc16">На что уходит время?</h4>

<p>Одно время я интересовался скоростью работы скриптов, с целью прикинуть пути оптимизации.
Однако, более или менее точно определить скорость работы скрипта не представлялось возможным, так
как у нас нет доступа к системному времени.
Однако когда появился NewFlash от Grey, я воспользовался возможностью замерять время затрачиваемое
на выполнение разных тестов.
Я не расскажу сейчас о всех интересных моментах, скажу лишь о главном на мой взгляд.</p>

<p>После множества тестов, становится очевидным, что львиная часть времени затрачивается
на постоянный парсинг. Причем зачастую повторый парсинг одного и того же кода.
Как-то я пытался соптимизировать проверку дистанции между двумя точками на плоскости:</p>

<pre class="hl sqf">00 funcNearestFromList = {
01     private ["_nearest", "_mindistq", "_distq", "_pos", "_px", "_py"];
02     // Квадрат минимальной найденной дистанции
03     _mindistq = if (count _this &gt; 2) then {(arg(2)) ^ 2} else {1e+10};
04     // Найденный объект
05     _nearest = objNull;
06     // X и Y координаты заданной позиции
07     _px = arg(0) select 0;
08     _py = arg(0) select 1;
09     // Для всех переданных объектов (два умножения на итерацию)
10     {
11         // Позиция очередного объекта (_pos) и квадрат
12         // расстояния от объекта до заданной позиции (_distq)
13         _pos = getpos _x;
14         _distq = (_px - (_pos select 0)) ^ 2 + (_py - (_pos select 1)) ^ 2;
15         // Если текущий объект ближе предыдущего
16         if ( _mindistq &gt; _distq ) then {
17             // то обновить данные
18             _mindistq = _distq;
19             _nearest = _x
20         }
21     } foreach arg(1);
22     // Вернуть найденный объект
23     _nearest
24 };</pre>
<p>Здесь избегается достаточно тяжелая оперция нахождения квадратного корня, и, в любом
другом языке, даже интерпретируемом, это даёт некоторый, иногда ощутимый, прирост скорости.
Однако, я был удивлен обаружив, что родной distance, работает в среднем в 12 раз
быстрее тринадцатой и четырнадцатой строк этого примера.
И это при том, что distance работает в трех измерениях и естественно использует sqrt.
Оба варианта запускались во вложенных циклах выполняющихся несколько тысяч раз.</p>

<p>Даже лишние скобки сказывались на быстродействии, это была очень незначительная разница, но она стабильно повторялась от теста к тесту.</p>

<p>Основной вывод который можно сделать из этого факта — не оптимизируйте на низком уровне, оптимизируйте сам подход, алгоритм.</p>

<h4 id="toc17">Сортировка</h4>

<p>Действительно, сортировка редко бывает нужна, но все таки мне встречались
пару раз скрипты использующие её. Это была, естественно, bubble sort. её отличительная особенность —
это ужасная зависимость скорости от объема сортируемых данных, которая выражается как N².
Когда мне понадобился сортированный вывод строк в диалоги я решил подстраховаться и использовать что-то более быстрое.
(Сравнение строк — это еще одна проблема, которую, впрочем можно решить для некотрых случаев)
Быстрая сортировка Хоара очень плохо реализуется средствами sqf так как требует рекурсии,
и моим выбором стала heap sort, столь же быстрая и более удобная в реализации.</p>

<p>Поскольку это не самый известный алгоритм, я привожу его здесь:</p>

<pre class="hl sqf">/*
    Сортировка на пирамиде, O(n log n)
    модифицирует переданный массив
*/

private ["_sift", "_arr", "_max", "_i", "_l", "_u", "_c", "_tmp"];

#define incSwapCounter _swapCounter = _swapCounter + 1
#define sift(a,b)  _l=(a); _u=(b); call _sift

_sift = {
    _tmp = _arr select _l;
    while {
        _c = _l+_l+1;
        if (_c &lt;= _u) then {
            if (_c &lt; _u) then {
                if ( _arr select _c+1 &gt; _arr select _c ) then {
                    _c = _c + 1;
                };
            };
            if (_tmp &lt; _arr select _c) then {
                _arr set [_l, _arr select _c];
                _l = _c;
                incSwapCounter;
                <em>true</em>
            }
        }
    } do {};
    _arr set [_l, _tmp];
};

_arr = _this;
_max = count _arr - 1;

_i = _max/2 call {_this - _this % 1};

while { _i &gt;= 0 } do { sift(_i,_max); _i = _i-1 };

_i = _max;

while { _i &gt; 0 } do {

    _tmp = _arr select 0;
    _arr set [0, _arr select _i];
    _arr set [_i, _tmp];
    incSwapCounter;

    sift(0,_i-1); _i = _i-1
};

_arr</pre>
<p>Следующая таблица показывает скорость работы сортировок на пирамиде и пузырьками
(количество перестановок и время работы (clock из NewFlash)):</p>

<pre>количество сортируемых элементов: 10
sort\bubbleSort.sqf:    swap: 25      time: 0
sort\hsort.v2.4.sqf:    swap: 27      time: 0.0159912

количество сортируемых элементов: 50
sort\bubbleSort.sqf:    swap: 574     time: 0.0939941
sort\hsort.v2.4.sqf:    swap: 242     time: 0.0469971

количество сортируемых элементов: 100
sort\bubbleSort.sqf:    swap: 2724    time: 0.360001
sort\hsort.v2.4.sqf:    swap:  582    time: 0.108994

количество сортируемых элементов: 200
sort\bubbleSort.sqf:    swap: 10253   time: 1.39102
sort\hsort.v2.4.sqf:    swap:  1347   time: 0.218994

количество сортируемых элементов: 300
sort\bubbleSort.sqf:    swap: 22880   time: 3.25
sort\hsort.v2.4.sqf:    swap:  2209   time: 0.390015

количество сортируемых элементов: 400
sort\bubbleSort.sqf:    swap: 37962   time: 5.59402
sort\hsort.v2.4.sqf:    swap:  3120   time: 0.547028

количество сортируемых элементов: 500
sort\bubbleSort.sqf:    swap: 63920   time: 9.172
sort\hsort.v2.4.sqf:    swap:  4020   time: 0.625
</pre>
<p>Конечно heap sort содержит больше кода, но отрыв происходит уже на массиве в 50 элементов (или чуть раньше).
Если бы код выполнялся быстрее, а сравнение обходилось бы дороже, то heap sort выглядела бы еще привлекательнее.</p>

<p>Код сравниваемых сортировок и скрипт теста лежит в архиве.</p>

<div class="author">Copyright © <a href="https://code.google.com/u/114481294814027320015/" rel="author">DenVdmj</a>, 2006–2014.</div>
</body>
</html>